/*
 * Copyright 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
}

kotlin {
    explicitApi()
}

android {
    namespace 'com.google.android.glance.tools.preview'
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32

        consumerProguardFiles "consumer-rules.pro"

        vectorDrawables {
            useSupportLibrary true
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        buildConfig false
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.composeCompiler.get()
    }
    lintOptions {
        textReport true
        textOutput 'stdout'
        // We run a full lint analysis as build part in CI, so skip vital checks for assemble tasks
        checkReleaseBuilds false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        animationsDisabled true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    packagingOptions {
        // Exclude license files to enable our test APK to build (has no effect on our AARs)
        excludes += "/META-INF/AL2.0"
        excludes += "/META-INF/LGPL2.1"
    }

    publishing {
        multipleVariants {
            allVariants()
        }
    }
}

dependencies {
    compileOnly libs.glance.appwidgets

    implementation libs.androidx.core
    implementation libs.androidx.activity.compose
    implementation libs.compose.ui.ui
    implementation libs.compose.ui.tooling.preview
    implementation libs.compose.material.material3
    implementation libs.compose.material.material

    debugImplementation libs.compose.ui.tooling
    debugImplementation libs.compose.ui.test.manifest

    // ======================
    // Test dependencies
    // ======================

    androidTestUtil libs.androidx.test.orchestrator

    androidTestImplementation libs.androidx.activity.compose
    androidTestImplementation libs.compose.material.material

    androidTestImplementation libs.junit
    androidTestImplementation libs.truth

    androidTestImplementation libs.compose.ui.test.junit4
    androidTestImplementation libs.compose.ui.test.manifest
    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.test.uiAutomator
}

apply plugin: "com.vanniktech.maven.publish"